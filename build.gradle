plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
  id "org.jetbrains.kotlin.jvm" version "1.9.24"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/dungeon_utils.accesswidener")
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.ladysnake.org/releases" }
    maven { url = "https://mvn.devos.one/releases/" }
    maven { url = "https://cursemaven.com" }
    maven { url = "https://jm.gserv.me/repository/maven-public/" }
    maven { url = "https://maven.kyrptonaught.dev" }
    maven { url = "https://maven.kosmx.dev/" }
    maven { url = "https://maven.zigythebird.com/mods" }
    maven { url = "https://libs.azuredoom.com:4443/mods" }
    maven { url = "https://jitpack.io" }
}

/**
 *
 * Mods in .jar form:
 * amendments-1.20-1.2.8-fabric.jar
 * appleskin-fabric-mc1.20.1-2.5.1.jar
 * authme-7.0.2+1.20.jar
 * bobby-5.0.1.jar
 * e4mc_minecraft-5.1.0.jar
 * embeddium-fabric-0.3.25+mc1.20.1.jar
 * entity_model_features_fabric_1.20.1-2.1.3.jar
 * entity_texture_features_fabric_1.20.1-6.1.3.jar
 * iris-1.7.2+mc1.20.1.jar
 * moonlight-1.20-2.12.9-fabric.jar
 * nvidium-0.2.6-beta.jar
 * supplementaries-1.20-2.8.17-fabric.jar
 * suppsquared-1.20-1.1.14-fabric.jar
 *
 */
dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings fileTree(dir: "mapping", include: "yarn-1.20.1+build.local.jar")
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Player Anim
    modImplementation "dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2-rc1+1.20"
    include "dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2-rc1+1.20"
    modImplementation "com.zigythebird.multiloaderutils:zigysmultiloaderutils-fabric-${project.minecraft_version}:1.2.4"
    include "com.zigythebird.multiloaderutils:zigysmultiloaderutils-fabric-${project.minecraft_version}:1.2.4"
    modImplementation "com.zigythebird.playeranimatorapi:playeranimatorapi-fabric-${project.minecraft_version}:2.0.5"
    include "com.zigythebird.playeranimatorapi:playeranimatorapi-fabric-${project.minecraft_version}:2.0.5"

    // Midnight Lib
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"

    // Mod menu
    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Trinkets
    modImplementation "dev.emi:trinkets:${project.trinkets_version}"

    // Pehkui
    modImplementation "com.github.Virtuoel:Pehkui:${project.pehkui_version}", {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Entity Hitboxes
    modImplementation "maven.modrinth:mecha-api:${project.mecha_api_version}"

    // Main Menu Credits
    modImplementation "maven.modrinth:main-menu-credits:${project.main_menu_credits_version}"

    // Porting Lib
    modImplementation "io.github.fabricators_of_create.Porting-Lib:${project.porting_lib_module}:${project.porting_lib_version}"

    modImplementation "curse.maven:modmenu-badges-lib-914586:4758379"

    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    // Curse Maven Mavity Lib
    modImplementation "curse.maven:mavity_lib-1016352:${project.mavity_lib_version_curseforge}"

    modImplementation "curse.maven:auudio-fabric-613072:4581988"

    modImplementation "curse.maven:fabric-shield-lib-380649:4955668"
    include "curse.maven:fabric-shield-lib-380649:4955668"

    modImplementation "com.github.Chocohead:Fabric-ASM:v${project.fabricasm_version}"
    include "com.github.Chocohead:Fabric-ASM:v${project.fabricasm_version}"

    modImplementation "curse.maven:cosmetic-lib-1019553:5429299"
    include "curse.maven:cosmetic-lib-1019553:5429299"

    modImplementation "curse.maven:better-third-person-435044:4596636"
    modImplementation "curse.maven:jade-324717:5339230"
    modImplementation "curse.maven:jei-238222:5419573"

    modImplementation group: "info.journeymap", name: "journeymap-api", version: "1.20-1.9-fabric-SNAPSHOT", changing: true
    modRuntimeOnly "curse.maven:journeymap-32274:5457845"

    modImplementation "curse.maven:xaeros-minimap-263420:5394774"
    modImplementation "curse.maven:xaeros-world-map-317780:5394838"

    modImplementation 'net.kyrptonaught:customportalapi:0.0.1-beta64-1.20'
    include 'net.kyrptonaught:customportalapi:0.0.1-beta64-1.20'

    modImplementation "curse.maven:lock-on-refabricated-1052671:5484917"
    include "curse.maven:lock-on-refabricated-1052671:5484917"

    // Cardinal Components API
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}"

    // Magic Bar Lib
    modImplementation "curse.maven:magicbarlib-1075641:5588452"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "loader_version", loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": minecraft_version,
                "loader_version": loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    // Only works if the Java version is Java 17 or higher, to avoid issues.
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        throw new RuntimeException("Dungeon Utils needs Java 17 or above. Current Java version is " + JavaVersion.current())
    }
}